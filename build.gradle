apply from: 'buildsystem/dependencies.gradle'
// Top-level build file where you can add configuration options common to all sub-projects/modules.

//COMMON BUILD TOOL AND SDK VERSION FOR ALL MODULES
ext {
    VERSION_NAME='3.4.0-SNAPSHOT' //GLOBAL VERSION FOR REDUKS libs
    POM_NAME='Reduks: Reduxjs for Kotlin+Android'
    GROUP_MAVEN_PUSH= 'com.github.beyondeye.reduks' //used by maven-push: MAKE SURE THAT THIS MATCHES group DEFINED BELOW IN THIS FILE AND USED BY JITPACK
    //TODO: decide when to migrate to 1.8: see https://developer.android.com/studio/write/java8-support.html
    sourceCompatibility_ = 1.6 //I want this library to be linkable from android projects
    targetCompatibility_ = 1.6 //I want this library to be linkable from android projects
}

allprojects {
   repositories {
       jcenter() //for coroutines
//       mavenCentral()
       google()
       maven { url "https://jitpack.io" }
   }
    configurations {
        javadocDeps
    }

    //Test Logging
    tasks.withType(Test) {
        testLogging {
            events "started", "passed", "skipped", "failed"
        }
    }
}
//generic configuration for kotlin subprojects (not used any more: now each subprojects contains its configuration)
configure(subprojects.findAll{it.name =='some-kotlin-module' ||it.name =='some-other-kotlin-module'}) {

    apply plugin: 'kotlin'
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: 'maven'
    group = 'com.github.beyondeye.reduks' //required by jitpack

    compileKotlin {
//        sourceCompatibility = JavaVersion.VERSION_1_8
        sourceCompatibility = sourceCompatibility_ //I want this library to be linkable from android projects
//        targetCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = targetCompatibility_ //I want this library to be linkable from android projects
//        kotlinOptions.jvmTarget = "1.8"
    }
    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
    }
    javadoc.dependsOn dokka

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    dependencies {
        implementation libraries.kotlinStdLib
        apiElements libraries.kotlinStdLib
        testImplementation libraries.junit
        testImplementation libraries.assertJ
        testImplementation libraries.rxJava
    }
}

//generic configuration for pure java subprojects (not used any more: now each subproject contains its configuration)
configure(subprojects.findAll{it.name =='some-pure-java-module'}) {

    apply plugin: 'java'
    apply plugin: 'maven'
    group = 'com.github.beyondeye.reduks' //required by jitpack


    sourceCompatibility =sourceCompatibility_
    targetCompatibility =targetCompatibility_

    dependencies {
        testImplementation libraries.junit
        testImplementation libraries.assertJ
    }
}

//generic configuration for android kotlin subprojects (not used any more: now each subproject contains its configuration)
configure(subprojects.findAll{it.name =='code_fragments' || it.name =='reduks-devtools' || it.name =='reduks-kovenant' || it.name =='reduks-rx'}) {

    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'org.jetbrains.dokka-android'
    apply plugin: 'com.github.dcendents.android-maven'
    group = 'com.github.beyondeye.reduks' //required by jitpack

    android {
        compileSdkVersion versions.compileSdk
//        buildToolsVersion androidBuildToolsVersion

        defaultConfig {
            minSdkVersion versions.minSdk
            targetSdkVersion versions.targetSdk
            versionCode = 1
            versionName VERSION_NAME
        }

        compileOptions {
            sourceCompatibility sourceCompatibility_
            targetCompatibility targetCompatibility_
        }
        sourceSets {
            main.java.srcDirs += 'src/main/kotlin'
            test.java.srcDirs += 'src/test/kotlin'
        }
        lintOptions {
            abortOnError false
        }
    }
    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/documentation"
    }
    dependencies {
        implementation libraries.kotlinStdLib
        //api libraries.kotlinStdLib

        testImplementation libraries.junit
        testImplementation libraries.assertJ
        testImplementation libraries.robolectric
        testImplementation libraries.equalsverifier
    }
}
buildscript {
    repositories {
        jcenter()
        google()
//        mavenCentral()
    }
    rootProject.ext.versions = [
            kotlin: '1.3.31',
            dokka: '0.9.18'
    ]

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$rootProject.versions.kotlin"
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.3'
        classpath "com.github.dcendents:android-maven-gradle-plugin:2.0"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$rootProject.versions.dokka"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:$rootProject.versions.dokka"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

////task for updating gradle
//task wrapper(type: Wrapper) {
//    gradleVersion = '4.10.1'
//}
