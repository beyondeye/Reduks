// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath "com.netflix.nebula:gradle-extra-configurations-plugin:2.2.+"
        classpath "com.github.dcendents:android-maven-gradle-plugin:$dcendents_maven_plugin_version"

    }
}
allprojects {

    configurations {
        javadocDeps
    }
    //COMMON BUILD TOOL AND SDK VERSION FOR ALL MODULES
    ext {
        VERSION_NAME='2.0.0b7-SNAPSHOT' //GLOBAL VERSION FOR REDUKS libs
        POM_NAME='Reduks: Reduxjs for Kotlin+Android'
        GROUP_MAVEN_PUSH= 'com.github.beyondeye.reduks' //used by maven-push: MAKE SURE THAT THIS MATCHES group DEFINED BELOW IN THIS FILE AND USED BY JITPACK
        sourceCompatibility_ = 1.6 //I want this library to be linkable from android projects
        targetCompatibility_ = 1.6 //I want this library to be linkable from android projects
        compileSdkVersion_ = 24
        minSdkVersion_ = 15 //Integer
        targetSdkVersion_ = 24 //Integer
        extraConfVersion = '2.2.+'
    }
}


//config for android specific libs
//configure(subprojects.findAll {it.name != 'reduks-core' && it.name != 'zjsonpatch'}) {
//
//}
//config for java specific libs
//configure(subprojects.findAll {it.name == 'reduks-core' || it.name == 'zjsonpatch'}) {
//
//}
configure(project(':reduks-core')) {
    repositories {
        mavenCentral()
    }
    apply plugin: 'kotlin'
    apply plugin: 'maven'
    group = 'com.github.beyondeye.reduks' //required by jitpack

    sourceCompatibility = sourceCompatibility_ //I want this library to be linkable from android projects
    targetCompatibility = targetCompatibility_ //I want this library to be linkable from android projects

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        testCompile "junit:junit:$junit_version"
        testCompile 'org.assertj:assertj-core:1.7.1'
        testCompile "io.reactivex:rxjava:$rxjava_version"
    }

}
configure(subprojects - project(':reduks-core')) {
    repositories {
        mavenCentral()
    }
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'com.github.dcendents.android-maven'
    group = 'com.github.beyondeye.reduks' //required by jitpack

    android {
        compileSdkVersion compileSdkVersion_
        buildToolsVersion androidBuildToolsVersion

        defaultConfig {
            minSdkVersion minSdkVersion_
            targetSdkVersion targetSdkVersion_
            versionCode = 1
            versionName VERSION_NAME
        }

        compileOptions {
            sourceCompatibility sourceCompatibility_
            targetCompatibility targetCompatibility_
        }
        sourceSets {
            main.java.srcDirs += 'src/main/kotlin'
            test.java.srcDirs += 'src/test/kotlin'
        }
        lintOptions {
            abortOnError false
        }
    }
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        testCompile "junit:junit:$junit_version"
        testCompile 'org.assertj:assertj-core:1.7.1'
        // testCompile "com.google.truth:truth:0.28"
        testCompile 'nl.jqno.equalsverifier:equalsverifier:2.0.2'
        testCompile "org.robolectric:robolectric:$robolectric_version"
    }
    //Test Logging
    tasks.withType(Test) {
        testLogging {
            events "started", "passed", "skipped", "failed"
        }
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}

//task for updating gradle
task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}
